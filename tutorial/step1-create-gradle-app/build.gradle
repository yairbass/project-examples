buildscript {
	ext {
		springBootVersion = '1.3.7.RELEASE'
	}
	repositories {
		maven {
			url System.getenv().artifactory_plugins_url
			credentials {
				username = System.getenv().artifactory_user
				password = System.getenv().artifactory_password
			}
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE")
	}
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

group = 'com.jfrog'
version = addBuildNumberFromCI('0.1.0')

processResources {
	filesMatching("**/application.properties") {
		expand version: version
	}
}

jar {
	baseName = 'frogsws'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	maven {
		url System.getenv().artifactory_libs_url
		credentials {
			username = System.getenv().artifactory_user
			password = System.getenv().artifactory_password
		}
	}
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.codehaus.groovy:groovy')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}


eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.3'
}

String addBuildNumberFromCI(String version) {
	def buildNumber = System.getenv().run_number
	return buildNumber ? version + "-" + buildNumber : version
}
